apply plugin: 'com.android.application'
apply plugin: 'com.apollographql.android'

android {
    signingConfigs {
        config {
            keyAlias key_alias
            keyPassword store_password
            storeFile file(store_path)
            storePassword store_password
        }
    }
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.ekylibre.android"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 12
        versionName "0.9.8"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        manifestPlaceholders = [
                'appAuthRedirectScheme': 'agrico'
        ]
        resValue 'string', 'mapbox_token', mapbox_token
//        javaCompileOptions {
//            annotationProcessorOptions {
//                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
//            }
//        }
        signingConfig signingConfigs.config
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "API_URL", eky_api_url
            //debuggable true
        }
        debug {
            buildConfigField "String", "API_URL", eky_debug_api_url
        }
    }
    flavorDimensions "version"
    productFlavors {
        ekylibre {
            dimension "version"
            applicationId 'com.ekylibre.android'
            versionNameSuffix "-eky"
            buildConfigField 'boolean', 'IS_AGRICO', 'false'
            ext {
                client_id = [debug: eky_debug_client_id, release: eky_client_id]
                client_secret = [debug: eky_debug_client_secret, release: eky_client_secret]
            }
            signingConfig signingConfigs.config
        }
        agrico {
            dimension "version"
            applicationId "com.ekylibre.android.agriconomie"
            versionNameSuffix "-agrico"
            buildConfigField 'boolean', 'IS_AGRICO', 'true'
            ext {
                client_id = [debug: agrico_debug_client_id, release: agrico_client_id]
                client_secret = [debug: agrico_debug_client_secret, release: agrico_client_secret]
                api_agrico = [debug: agrico_debug_api_url, release: agrico_api_url]
            }
            signingConfig signingConfigs.config
        }
    }
    applicationVariants.all { variant ->
        def flavor = variant.productFlavors[0]
        println "Setting up CLIENT_ID ${flavor.ext.client_id[variant.buildType.name]} for variant [${variant.name}]"
        variant.buildConfigField "String", "CLIENT_ID", "\"${flavor.ext.client_id[variant.buildType.name]}\""
        variant.buildConfigField "String", "CLIENT_SECRET", "\"${flavor.ext.client_secret[variant.buildType.name]}\""
        if (flavor.name == 'agrico') {
            variant.buildConfigField "String", "API_AGRICO", "\"${flavor.ext.api_agrico[variant.buildType.name]}\""
        }
    }
}

apollo {
    customTypeMapping['Date'] = "java.util.Date"
    customTypeMapping['Polygon'] = "com.mapbox.geojson.Polygon"
    customTypeMapping['Point'] = "com.mapbox.geojson.Point"
}

ext.supportLibraryVersion = "27.1.1"

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // Support libraries
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:exifinterface:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:support-fragment:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:support-vector-drawable:$supportLibraryVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    // Test libraries
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    // Google Services, needed for fixing SSL Handshake error
    implementation 'com.google.android.gms:play-services-base:15.0.1'
    // Room database
    implementation 'android.arch.persistence.room:runtime:1.1.1'
    annotationProcessor 'android.arch.persistence.room:compiler:1.1.1'
    // Retrofit & Picasso
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.4.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    // Apollo GraphQL Client
    implementation 'com.apollographql.apollo:apollo-runtime:0.5.0'
    implementation 'com.apollographql.apollo:apollo-android-support:0.5.0'
    // Mapbox
    implementation 'com.mapbox.mapboxsdk:mapbox-android-sdk:6.2.1'
    implementation 'com.mapbox.mapboxsdk:mapbox-sdk-services:3.3.0'
    implementation 'com.mapbox.mapboxsdk:mapbox-sdk-turf:3.3.0'
    api 'com.google.guava:guava:25.1-android'
    implementation 'com.jakewharton:process-phoenix:2.0.0'
    // Flavor specific libraries for Agriconomie.com
    agricoImplementation 'net.openid:appauth:0.7.1'
}
